module pupremote
author unknown
version 1 0 
description ''
variables _pup_commands tmp _pup_nr_modes _pup_cur_mode _pup_last_nack _pup_connected _pup_senddata _pup_payloads 

  spec ' ' 'add_command' 'add_command _ to_hub _ from_hub _ type _' 'auto auto auto auto' '10' '10' 10 '10'
  spec 'r' '_hextolist' '_hextolist _' 'auto' '10'
  spec ' ' '_init_sensor' '_init_sensor'
  spec ' ' '_initmode' '_initmode _' 'auto' '10'
  spec ' ' '_sendatachk' '_sendatachk _' 'auto' '10'
  spec ' ' '_sendhex' '_sendhex _' 'auto' '10'
  spec 'r' '_calc_len_data' '_calc_len_data _ _ _' 'auto auto auto' '10' '10' '10'
  spec 'r' '_get_type' '_get_type _' 'auto' '10'
  spec 'r' '_log_len' '_log_len _' 'auto' '10'
  spec 'r' '_heartbeat' '_heartbeat'
  spec 'r' '_log2' '_log2 _' 'auto' '10'
  spec ' ' '_reset' '_reset'
  spec ' ' '_send_data' '_send_data _' 'auto' '10'
  spec ' ' 'process' 'process'
  spec ' ' '_set_payload' '_set_payload _ _' 'auto auto' '10' '10'
  spec ' ' 'init' 'init'
  spec ' ' 'reset_nack' 'reset_nack'

to '_calc_len_data' data_type to_hub from_hub {
  return ('_log_len' ((1 << data_type) * (maximum to_hub from_hub)))
}

to '_get_type' type {
  local 'data_type' 0
  if (type == 'i') {
    data_type = 2
  } (type == 'w') {
    data_type = 1
  } else {
    data_type = 0
  }
  return data_type
}

to '_log_len' len {
  local 'loglen' 0
  if (len > 16) {
    return 5
  } (len > 8) {
    return 4
  } (len > 4) {
    return 3
  } (len > 2) {
    return 2
  } (len > 1) {
    return 1
  } else {
    return 0
  }
}

to add_command cmd to_hub from_hub type {
  comment 'type is dropdown ''B'', ''W'', ''L'' for byte, 16-bit word, 32-bit int'
  local 'data_type' ('_get_type' type)
  local 'new_command' ('[data:makeList]' cmd to_hub from_hub data_type ('_calc_len_data' data_type to_hub from_hub))
  if ((size _pup_commands) == 0) {
    _pup_commands = ('[data:makeList]' new_command)
  } else {
    '[data:addLast]' new_command _pup_commands
  }
  _pup_nr_modes = (size _pup_commands)
  '[data:addLast]' (newList 32) _pup_payloads
}

to _heartbeat {
  if ((('[serial:millis]') - _pup_last_nack) > 4000) {_reset}
  if (('[serial:available]') > 0) {
    graphIt (('[serial:millis]') - _pup_last_nack) ''
    local 'cmd' (at 1 ('[serial:readNr]' 1))
    if (cmd == 2) {
      _pup_last_nack = ('[serial:millis]')
      _sendatachk ('[data:makeList]' ((hexToInt '46') | 0) 0)
    } (cmd == (hexToInt '46')) {
      _pup_last_nack = ('[serial:millis]')
      local 'ser_data' ('[serial:read]')
      comment 'mode = (b & 0b111)'
      local 'wrtmode' ((at 3 ser_data) & 7)
      _pup_cur_mode = wrtmode
      comment ' len = 2 ** ((b & 0b111000) >> 3)'
      local 'lenpow2' (((at 3 ser_data) & 56) >> 3)
      local 'len' (1 << lenpow2)
      local 'list' ('[data:makeList]')
      for i len {
        '[data:addLast]' (at (i + 3) ser_data) list
      }
      graphIt (('[serial:millis]') - _pup_last_nack) (at (i + 3) ser_data) ''
      return ('[data:makeList]' wrtmode list)
    } (cmd == (hexToInt '43')) {
      _pup_last_nack = ('[serial:millis]')
      local 'ser_data' ('[serial:read]')
      _pup_cur_mode = (at 1 ser_data)
      sayIt 'mode' _pup_cur_mode
      waitMillis 5
      _send_data _pup_cur_mode
    }
  }
}

to _hextolist hexstr {
  local 'len' ((size hexstr) / 2)
  local 'data' (newList len)
  for i len {
    atPut i data (hexToInt ('[data:copyFromTo]' hexstr ((i * 2) - 1) (i * 2)))
  }
  return data
}

to init {
  comment 'Initialize global variables'
  _pup_commands = ('[data:makeList]')
  _pup_cur_mode = 0
  _pup_senddata = (newList 32)
  _pup_payloads = ('[data:makeList]')
  _pup_last_nack = ('[serial:millis]')
}

to _init_sensor {
  _sendhex 0
  comment 'TYPE_ID'
  _sendhex '403e81'
  comment 'CMD_MODES 0x49 <nr_modes> <nr_views>'
  _sendatachk ('[data:makeList]' (hexToInt '49') (_pup_nr_modes - 1) (_pup_nr_modes - 1))
  comment 'set baud rate to 115200'
  _sendhex '5200c201006e'
  comment 'CMD_VERSION'
  _sendhex '5f0000000100000105a5'
  for i _pup_nr_modes {
    _initmode ((_pup_nr_modes + 0) - i)
  }
}

to _initmode mode {
  local 'cur_command' (at (mode + 1) _pup_commands)
  local 'data_type' (at 4 cur_command)
  comment 'INFO mode + power'
  local 'list' ('[data:join]' ('[data:makeList]' ((hexToInt 'a0') | mode)) (_hextolist ('[data:toString]' '00646174')) ('[data:makeList]' (hexToInt '30') (48 + mode)) (_hextolist ('[data:toString]' '0080000000050400000000')))
  waitMillis 20
  _sendatachk list
  comment 'INFO RAW, PCT, SI'
  for i 3 {
    local 'list' ('[data:join]' ('[data:makeList]' ((hexToInt '98') | mode) i) (_hextolist ('[data:toString]' '000000000000c842')))
    _sendatachk list
    waitMillis 20
  }
  waitMillis 10
  comment 'INFO UNITS = empty len 0'
  local 'list' ('[data:makeList]' ((hexToInt '80') | mode) 4 0)
  _sendatachk list
  comment 'info mapping: 0x88|mode 0x05 input=(abs)0x10 output=(abs)0x10'
  local 'list' ('[data:makeList]' ((hexToInt '88') | mode) 5 (hexToInt '10') (hexToInt '10'))
  _sendatachk list
  comment '0x90|mode 0x80 <data_type> <log2(len)> <digits> <decimals>'
  local 'max_len' (maximum (at 2 cur_command) (at 3 cur_command))
  local 'list' ('[data:makeList]' ((hexToInt '90') | mode) (hexToInt '80') max_len (at 4 cur_command) 3 0)
  _sendatachk list
}

to _log2 len {
  if (len == 1) {
    return 0
  } (len == 2) {
    return 1
  } (len == 4) {
    return 2
  } (len == 8) {
    return 3
  } (len == 16) {
    return 4
  } (len == 32) {
    return 5
  } else {
    return 0
  }
}

to process {
  local 'local_data' (_heartbeat)
  local 'data_from_hub' (booleanConstant false)
  if ((size local_data) > 0) {
    comment 'arguments passed'
    data_from_hub = (booleanConstant true)
    local 'mode' (at 1 local_data)
    local 'payload' (at 2 local_data)
  } else {
    local 'mode' _pup_cur_mode
    local 'payload' ('[data:makeList]')
  }
  local 'function_name' (at 1 (at (mode + 1) _pup_commands))
  local 'has_return' ((at 2 (at (mode + 1) _pup_commands)) > 0)
  local 'has_arg' ((at 3 (at (mode + 1) _pup_commands)) > 0)
  if (and (not has_arg) has_return) {
    _set_payload mode (callCustomReporter function_name payload)
  } (and has_arg (and has_return data_from_hub)) {
    _set_payload mode (callCustomReporter function_name payload)
  } (and has_arg (and (not has_return) data_from_hub)) {
    callCustomCommand function_name payload
  } else {
  }
  _send_data _pup_cur_mode
}

to _reset {
  '[serial:close]'
  _pup_last_nack = ('[serial:millis]')
  _pup_cur_mode = 0
  _pup_connected = 0
  local 'tmp_var' 0
  if (('[serial:espversion]') == 2) {
    tmp_var = (digitalReadOp 8)
    digitalWriteOp 7 true
    waitMillis 50
    digitalWriteOp 7 false
  } else {
    '[serial:close]'
    tmp_var = (digitalReadOp 18)
    digitalWriteOp 19 true
    waitMillis 50
    digitalWriteOp 19 false
  }
  waitMillis 450
  '[serial:open]' 2400
  _init_sensor
  _sendhex 4
  '[serial:open]' 115200
  waitMillis 10
  _sendhex 'c0003f'
  _pup_connected = 1
  '[serial:open]' 115200
}

to reset_nack {
  _pup_last_nack = ('[serial:millis]')
}

to _send_data mode {
  if (_pup_connected == 1) {
    local 'payload' (at (mode + 1) _pup_payloads)
    local 'loglen' (at 5 (at (mode + 1) _pup_commands))
    local 'list' ('[data:makeList]' (((hexToInt 'c0') | (loglen << 3)) | mode))
    for i (1 << loglen) {
      '[data:addLast]' (at i payload) list
    }
    _sendatachk list
  }
}

to _sendatachk data {
  local 'chk' 255
  for i (size data) {
    chk = (chk ^ (at i data))
  }
  '[data:addLast]' chk data
  '[serial:write]' data
}

to _sendhex hexstr {
  if (isType hexstr 'number') {
    local 'data' (newList 1)
    atPut 1 data hexstr
  } else {
    local 'len' ((size hexstr) / 2)
    local 'data' (newList len)
    for i len {
      atPut i data (hexToInt ('[data:copyFromTo]' hexstr ((i * 2) - 1) (i * 2)))
    }
  }
  '[serial:write]' data
  waitMillis 1
}

to _set_payload mode payload {
  atPut (mode + 1) _pup_payloads payload
}

